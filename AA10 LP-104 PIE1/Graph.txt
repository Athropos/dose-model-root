set terminal pngcairo enhanced font "Helvetica,10" fontscale 1.5 size 800, 800 
set output 'DispersionGraph.png'

# change the default colors to more pleasant ones and make the lines a little bit thicker
set style line 1 lc rgb '#8b1a0e' pt 4 ps 1.5 lt 1 lw 1 # --- red-solid
set style line 2 lc rgb '#3b518b' pt 4 ps 1.5 lt 1 lw 1 # --- blue-solid
set style line 3 lc 7 pt 9 ps 1.5 lt 1 lw 1 # --- black-solid

set style line 4 lc rgb '#8b1a0e' dt 2 pt 6 ps 1.5 lt 1 lw 1 # --- red-dashed
set style line 5 lc rgb '#3b518b' dt 2 pt 6 ps 1.5 lt 1 lw 1 # --- blue - dashed
set style line 6 lc 7 dt 2 pt 8 ps 1.5 lw 1 # --- black-dashed

#  put the border more to the background by applying it only on the left and bottom part and put it and the tics in gray
set style line 11 lc 7 lt 1
set border 11 back ls 11
set tics nomirror

# add a slight grid to make it easier to follow the exact position of the curves
set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

set title "" 

set y2tics autofreq #0, 0.2, 1;

set xlabel 'Emission Height (m)' 
set ylabel 'Relative Concetration (s/m^3)'
set y2label 'Relative Error %'

plot 'DispersionGraph.txt' u 1:2  t 'P50% ESS-0241704' w lp ls 1 axis x1y1, \
	'' 		   u 1:3  t 'P50% ROOT' w lp ls 2 axis x1y1, \
	'' 		   u 1:($4*100)  t 'Relative Error P50%' w lp ls 3 axis x1y2, \
	'' 		   u 1:5  t 'P95% ESS-0241704' w lp ls 4 axis x1y1, \
	'' 		   u 1:6  t 'P95% ROOT' w lp ls 5 axis x1y1, \
	'' 		   u 1:($7*100)  t 'Relative Error P95%' w lp ls 6 axis x1y2

